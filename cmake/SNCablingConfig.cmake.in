#.rst: SNCablingConfig
#
# Variables
# - SNCabling_VERSION: version number
# - SNCabling_INCLUDE_DIR: Path to SNCabling headers
#
# Components
# - devtools: available if the package was built with devtools
# - docs: available if the package was built with documentation
# - service: available if the Bayeux service for SNCabling was built
#

#----------------------------------------------------------------------
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#----------------------------------------------------------------------

set(SNCabling_VERSION "@SNCabling_VERSION@")

#-----------------------------------------------------------------------
# Start of CMake Generated
@PACKAGE_INIT@
# END OF CMAKE GENERATED

# Component Availability
set(SNCabling_devtools_FOUND @SNCABLING_WITH_DEVELOPER_TOOLS@)
set(SNCabling_docs_FOUND @SNCABLING_WITH_DOCS@)
set(SNCabling_service_FOUND @SNCABLING_WITH_SERVICE@)

#-----------------------------------------------------------------------
# Setup paths that clients may need
#
set_and_check(SNCabling_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")


#-----------------------------------------------------------------------
# Handle paths/targets for third party deps here...
#
include(CMakeFindDependencyMacro)

if(SNCabling_service_FOUND)
  find_dependency(Bayeux @Bayeux_VERSION@ EXACT)
endif()

# Find Boost after Bayeux, as latter should supply boost for us.
find_dependency(Boost @Boost_MAJOR_VERSION@.@Boost_MINOR_VERSION@ EXACT
  COMPONENTS filesystem date_time system)

#-----------------------------------------------------------------------
# Include the file listing all the imported targets.
# This is installed in the same location as us...
#
if(NOT SNCabling_TARGETS_LOADED)
  include("${CMAKE_CURRENT_LIST_DIR}/SNCablingTargets.cmake")
  set(SNCabling_TARGETS_LOADED 1)
endif()

#-----------------------------------------------------------------------
# - Auto check components
check_required_components(SNCabling)

